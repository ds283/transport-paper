model "gelaton"
 { name        = "Gelaton-like semicircle trajectory";
   description = "Gelaton-like model in Cartesian coordinates";
   citeguide   = "CppTransport user guide arXiv:16xx.yyyy";
   license     = "CC BY";
   revision    = 1;

   references  = [ "arXiv:16xx.yyyyy" ];
   urls        = [ "http://transportmethod.com" ];
 };

author "David Seery"
 { email     = "D.Seery@sussex.ac.uk";
   institute = "University of Sussex";
 };

author "David Mulryne"
 { email     = "D.Mulryne@qmul.ac.uk";
   institute = "Queen Mary, University of London";
 };

author "Mafalda Dias"
 { email     = "M.Dias@desy.de";
   institute = "DESY, Hamburg";
 };

author "Jonathan Frazer"
 { email     = "J.Frazer@desy.de";
   institute = "DESY, Hamburg";
 };

% specify which class template should be used with this model
templates
 { core           = "canonical_core";
   implementation = "canonical_mpi";
 };

% fix properties of the steppers
background
 { abserr   = 1E-12;
   relerr   = 1E-12;
   stepsize = 1E-10;
   stepper  = "runge_kutta_dopri5";
 };

perturbations
 { abserr   = 1E-18;
   relerr   = 1E-18;
   stepsize = 1E-15;
   stepper  = "runge_kutta_dopri5";
 };

% set up the fields
field x
 { latex = "x";
 };

field y
 { latex = "y";
 };

% set up the parameters
parameter eta_chi
 { latex = "\eta_\chi";
 };

parameter g_chi
 { latex = "g_\chi";
 };

parameter epsilon_s
 { latex = "\epsilon_s";
 };

subexpr uplift
 { latex = "V_0";
   value = 10;
 };

subexpr angular_tilt
 { latex = "V_\theta";
   value = - sqrt(2*epsilon_s) * atan(x/y);
 };

subexpr radial_mass
 { latex = "V_{R^2}";
   value = (1/2) * eta_chi * (sqrt(x^2 + y^2)/M_P^2 - 2)^2;
 };

subexpr radial_cubic
 { latex = "V_{R^3}";
%   value = (1/6) * g_chi * exp(-(sqrt(x^2 + y^2)-2)^2 / 0.0001) * (sqrt(x^2 + y^2)/M_P^2 - 2)^3;
   value = (1/6) * g_chi * (sqrt(x^2 + y^2)/M_P^2 - 2)^3;
 };

potential = (1E-10 * M_P^4) * (uplift + angular_tilt + radial_mass + radial_cubic);
